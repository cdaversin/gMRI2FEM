rule segment_refinements:
  input:
    reference="data/mri_processed_data/T1maps/{subject}_ses-01_T1map_hybrid.nii.gz",
    segmentation="data/mri_processed_data/freesufer/{subject}/mri/aparc+aseg.mgz",
    csfmask="data/mri_processed_data/{subject}/{subject}_csfmask.nii.gz",
  output:
    refined="data/mri_processed_data/{subject}/{subject}_aparc+aseg_refined.nii.gz",
    csf_segmentation="data/mri_processed_data/{subject}/{subject}_aparc+aseg_csfseg.nii.gz"
  shell:
    "python gmri2fem/analysis/segmentation_refinement.py"
    " --fs_seg {input.segmentation}"
    " --reference {input.reference}"
    " --csfmask {input.csfmask}"
    " --output_seg {output.refined}"
    " --output_csfseg {output.csf_segmentation}"


rule LookLocker_stats:
    input:
      data="data/mri_processed_data/{subject}/T1maps/{subject}_{session}_T1map_hybrid.nii.gz",
      segmentation="data/mri_processed_data/{subject}/{subject}_aparc+aseg_refined.nii.gz",
      timestamps="data/mri_dataset/derivatives/timetable.csv",
      lutfile="data/mri_processed_data/freesurfer_lut.json"
    output:
      "data/mri_processed_data/{subject}/statistics/{subject}_{session}_LookLocker_statstable.csv"
    shell:
      "python gmri2fem/analysis/region_quantities.py"
      " --subjectid {wildcards.subject}"
      " --subject_session {wildcards.session}"
      " --sequence LookLocker" #"T1map"
      " --data {input.data}"
      " --seg {input.segmentation}"
      " --timestamps {input.timestamps}"
      " --lutfile {input.lutfile}"
      " --output {output}"


rule LookLocker_collected:
  input:
    expand(
      "data/mri_processed_data/{{subject}}/statistics/{{subject}}_{session}_LookLocker_statstable.csv",
      session=SESSIONS
    )
  output:
    "data/mri_processed_data/{subject}/statistics/{subject}_LookLocker_statstable.csv",
  shell:
    "python gmri2fem/analysis/concat_tables.py"
    " --input {input}"
    " --output {output}"

rule T1w_stats:
    input:
      data="data/mri_processed_data/{subject}/registered/{subject}_{session}_T1w_registered.nii.gz",
      segmentation="data/mri_processed_data/{subject}/{subject}_aparc+aseg_refined.nii.gz",
      timestamps="data/mri_dataset/derivatives/timetable.csv",
      lutfile="data/mri_processed_data/freesurfer_lut.json"
    output:
      "data/mri_processed_data/{subject}/statistics/{subject}_{session}_T1w_statstable.csv"
    shell:
      "python gmri2fem/analysis/region_quantities.py"
      " --subjectid {wildcards.subject}"
      " --subject_session {wildcards.session}"
      " --sequence T1w"
      " --data {input.data}"
      " --seg {input.segmentation}"
      " --timestamps {input.timestamps}"
      " --lutfile {input.lutfile}"
      " --output {output}"


rule collected:
  input:
    expand(
      "data/mri_processed_data/{{subject}}/statistics/{{subject}}_{session}_{sequence}_statstable.csv",
      session=SESSIONS,
      sequence=["LookLocker", "T1w"]
    )
  output:
    "data/mri_processed_data/{subject}/statistics/{subject}_statstable.csv",
  shell:
    "python gmri2fem/analysis/concat_tables.py"
    " --input {input}"
    " --output {output}"


