rule T1map_estimation_from_LL:
  input:
    LL="data/mri_dataset/{subject}/{session}/anat/{subject}_{session}_acq-looklocker_IRT1.nii.gz",
    timestamps="data/mri_dataset/{subject}/{session}/anat/{subject}_{session}_acq-looklocker_IRT1_trigger_times.txt"
  output:
    T1raw="data/mri_dataset/derivatives/{subject}/{session}/{subject}_{session}_acq-looklocker_T1map.nii.gz",
    R1raw="data/mri_dataset/derivatives/{subject}/{session}/{subject}_{session}_acq-looklocker_R1map.nii.gz",
    T1="data/mri_dataset/derivatives/{subject}/{session}/{subject}_{session}_acq-looklocker_T1map_postprocessed.nii.gz",
    R1="data/mri_dataset/derivatives/{subject}/{session}/{subject}_{session}_acq-looklocker_R1map_postprocessed.nii.gz",
  shell:
    "python gmri2fem/mriprocessing/looklocker_to_T1map.py"
    " --input {input.LL}"
    " --timestamps {input.timestamps}"
    " --output {output.T1raw}"
    " --postprocessed {output.T1}"
    " --R1 {output.R1raw}"
    " --R1_postprocessed {output.R1}"

rule T1map_estimation_from_mixed:
  input:
    IR="data/mri_dataset/{subject}/{session}/mixed/{subject}_{session}_acq-mixed_IR-corrected-real.nii.gz",
    SE="data/mri_dataset/{subject}/{session}/mixed/{subject}_{session}_acq-mixed_SE-modulus.nii.gz",
    meta="data/mri_dataset/{subject}/{session}/mixed/{subject}_{session}_acq-mixed_meta.json"
  output:
    T1map="data/mri_dataset/derivatives/{subject}/{session}/{subject}_{session}_acq-mixed_T1map.nii.gz",
    T1map_post="data/mri_dataset/derivatives/{subject}/{session}/{subject}_{session}_acq-mixed_T1map_postprocessed.nii.gz",
  shell:
    "python gmri2fem/mriprocessing/estimate_mixed_t1maps.py"
    " --SE {input.SE}"
    " --IR {input.IR}"
    " --meta {input.meta}"
    " --output {output.T1map}"
    " --postprocessed {output.T1map_post}"

rule csfmask:
  input:
    "data/mri_processed_data/{subject}/registered/{subject}_ses-01_T2w_registered.nii.gz",
  output:
    "data/mri_processed_data/{subject}/{subject}_csfmask.nii.gz",
  shell:
    "python gmri2fem/mriprocessing/mask_csf.py"
    " --input {input}"
    " --output {output}"


rule hybrid_T1maps:
   input:
      ll="data/mri_processed_data/{subject}/registered/{subject}_{session}_acq-looklocker_T1map_registered.nii.gz",
      mixed="data/mri_processed_data/{subject}/registered/{subject}_{session}_acq-mixed_T1map_registered.nii.gz",
      csfmask="data/mri_processed_data/{subject}/{subject}_csfmask.nii.gz",
   output:
      T1map="data/mri_processed_data/{subject}/T1maps/{subject}_{session}_T1map.nii.gz",
   shell:
      "python gmri2fem/mriprocessing/hybrid_t1maps.py"
      " --ll {input.ll}"
      " --mixed {input.mixed}"
      " --csfmask {input.csfmask}"
      " --output {output}"
